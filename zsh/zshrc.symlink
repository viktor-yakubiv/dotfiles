# Setting locale when missing
if [[ -z "$LANG" ]]; then
	export LANG=en_US.UTF-8
fi

# Shortcut to this dotfiles path is $DOT_HOME
# It is in a transition to $ZSH after removal of oh-my-zsh
export DOT_HOME="$HOME/.dotfiles"
export ZSH="$DOT_HOME"

# Projects directory for development
export DEV_HOME="$HOME/Development"

# Custom theme to highlight everything
eval "$(starship init zsh)"

# Antigen is a package manager to load plugins
#
# The manager is installed via Homebrew.
# It can be invoked at any configuration file in this repository
# and also in .localrc
source /usr/local/share/antigen/antigen.zsh

# Plugins
antigen bundle git # I've got addicted to git shorthands
antigen bundle zsh-users/zsh-syntax-highlighting
antigen bundle zsh-users/zsh-autosuggestions

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# All of our zsh files
typeset -U config_files
config_files=($DOT_HOME/**/*.zsh)

# Load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if [[ "$PWD" = "$HOME" ]]; then
  cd "$DEV_HOME" > /dev/null
fi

# Finish loading plugins with Antigen
antigen apply
