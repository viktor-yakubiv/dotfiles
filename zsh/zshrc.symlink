# Setting locale when missing
if [ -z "$LANG" ]; then
	export LANG=en_US.UTF-8
fi

# Essential environment
export ZSH="$(dirname "$(readlink -f "$HOME/.zshrc")")"

# Smart dotfiles location resolution based on the link to current file
# Dotfiles located at ~/.config on MacOS
# but are elsewhere on Linux and GitHub Codespaces
export DOT_HOME="$(dirname "$ZSH")"


# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# All of our zsh files
typeset -U config_files
config_files=($DOT_HOME/**/*.zsh)
config_files=(${config_files:#$ZSH/*}) # local modules are sourced manually

# Load the env
# it can also alter PATH
source "$ZSH/env.zsh"
for file in ${(M)config_files:#*/env.zsh}; do
  source $file
done

# Load everything but the env and completion files
source "$ZSH/functions.zsh"
source "$ZSH/cdls.zsh"
for file in ${${config_files:#*/env.zsh}:#*/completion.zsh}; do
  source $file
done

# Initialize autocomplete here,
# otherwise functions won't be loaded
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

unset config_files

# Package manager and external dependecies
source "$ZSH/zgenom.zsh" # requires `compdef`

# Custom theme to highlight everything
eval "$(starship init zsh)"

if [[ "$PWD" = "$HOME" ]]; then
  cd "$DEV_HOME" > /dev/null
fi
