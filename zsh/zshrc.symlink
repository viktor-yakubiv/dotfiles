# Setting locale when missing
if [[ -z "$LANG" ]]; then
	export LANG=en_US.UTF-8
fi

# Setting up oh-my-zsh
export OMZ_HOME="$HOME/.oh-my-zsh"
export ZSH="$OMZ_HOME"


# Shortcut to this dotfiles path is $DOT_HOME
# This has to be so as Oh My ZSH highly depends on the ZSH env variable
export DOT_HOME="$HOME/.dotfiles"

# Projects directory for development
export DEV_HOME="$HOME/Development"

# Custom theme to highlight everything
eval "$(starship init zsh)"

# Standard plugins can be found in ~/.oh-my-zsh/plugins/*
# Custom plugins may be added to ~/.oh-my-zsh/custom/plugins/
# Example format: plugins=(rails git textmate ruby lighthouse)
# Add wisely, as too many plugins slow down shell startup.
plugins=(
  git
  npm
  brew
  sublime
  gh
)

source $ZSH/oh-my-zsh.sh

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# All of our zsh files
typeset -U config_files
config_files=($DOT_HOME/**/*.zsh)

# Load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if [[ "$PWD" = "$HOME" ]]; then
  cd "$DEV_HOME" > /dev/null
fi
